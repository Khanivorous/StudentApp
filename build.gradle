plugins {
    id 'java'
    id 'jacoco'
    id 'au.com.dius.pact' version '4.0.3'
}

group 'com.khanivorous.studentapp'
version '1.0-SNAPSHOT'
jar {
    manifest {
        attributes(
                'Main-Class': 'PrintStudentInformation'
        )
    }
}
sourceCompatibility = 1.8
// Versioning of dependencies
wrapper.gradleVersion = '5.5.1'
def cucumberVersion = '4.8.0'
def junitVersion = '5.5.2'
def okHttpVersion = '4.2.2'
def retrofitVersion ='2.6.2'
def mockitoVersion = '3.1.0'

repositories {
    jcenter()
    mavenCentral()
}

jacoco {
    toolVersion = "0.8.5"
}
jacocoTestReport {
    reports {
        xml.enabled true
        csv.enabled true
        html.destination file("${buildDir}/jacocoHtml")
    }
}

dependencies {

    implementation "com.squareup.okhttp3:okhttp:${okHttpVersion}"
    implementation "com.squareup.retrofit2:retrofit:${retrofitVersion}"
    implementation "com.squareup.retrofit2:converter-gson:${retrofitVersion}"
    implementation "com.squareup.retrofit2:adapter-rxjava2:${retrofitVersion}"

    testImplementation "com.squareup.okhttp3:mockwebserver:${okHttpVersion}"
    testImplementation "io.cucumber:cucumber-java:${cucumberVersion}"
    testImplementation "io.cucumber:cucumber-junit:${cucumberVersion}"
    testImplementation "au.com.dius:pact-jvm-consumer-junit:4.0.3"

    testImplementation "org.junit.jupiter:junit-jupiter-api:${junitVersion}"
    testImplementation "org.mockito:mockito-core:${mockitoVersion}"
    testRuntimeOnly "org.junit.jupiter:junit-jupiter-engine:${junitVersion}"
    testRuntimeOnly "org.junit.vintage:junit-vintage-engine:${junitVersion}"

}

test {
    useJUnitPlatform()
    testLogging {
        events "passed","skipped","failed"
    }
}

configurations {
    cucumberRuntime {
        extendsFrom testImplementation
    }
}

task cucumber() {
    dependsOn assemble, compileTestJava
    doLast {
        javaexec {
            main = "io.cucumber.core.cli.Main"
            classpath = configurations.cucumberRuntime + sourceSets.main.output + sourceSets.test.output
            args = ['--plugin', 'pretty', '--glue', 'cucumber', 'src/test/resources']
        }
    }
}

pact {
    publish {// defaults to $buildDir/pacts
    }
}